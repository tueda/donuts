plugins {
  id 'jacoco'
  id 'java-library'
  id 'maven-publish'
  id 'com.diffplug.gradle.spotless' version '3.27.1'
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'net.researchgate.release' version '2.8.1'
  id 'ru.vyarus.quality' version '4.1.0'
}

group = 'com.github.tueda'
archivesBaseName = 'donuts'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def versions = [
  rings: '2.5.7',
  checkstyle: '8.29',
  pmd: '6.21.0',
  spotbugs: '4.0.0',
]

repositories {
  mavenCentral()
}

dependencies {
  api "cc.redberry:rings:${versions.rings}"
  compileOnly "com.github.spotbugs:spotbugs-annotations:${versions.spotbugs}"
  compileOnly 'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'
  testImplementation 'com.google.truth:truth:1.0.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
  // for dependencyUpdates
  testRuntimeOnly "net.sourceforge.pmd:pmd:${versions.pmd}"
}

javadoc {
  options.with {
    source = '8'
    links += [
      "https://docs.oracle.com/javase/${source}/docs/api/".toString(),
      "https://static.javadoc.io/cc.redberry/rings/${versions.rings}/".toString(),
    ]
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat('1.7')
  }
  groovyGradle {
    greclipse().configFile('config/greclipse.properties')
  }
}

configurations {
  checkstyleConfig
}

dependencies {
  checkstyleConfig("com.puppycrawl.tools:checkstyle:${versions.checkstyle}") {
    transitive = false
  }
}

quality {
  checkstyleVersion = versions.checkstyle
  pmdVersion = versions.pmd
  spotbugsVersion = versions.spotbugs

  spotbugsEffort = 'max'
  spotbugsLevel = 'low'
}

afterEvaluate {
  checkstyle {
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  }
  pmd {
    incrementalAnalysis = true
    ruleSetFiles = files('config/pmd.xml')
  }
}

shadowJar {
  minimize()
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  archiveClassifier = 'javadoc'
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      pom {
        name = 'Donuts'
        description = 'A wrapper library for Rings'
        url = 'https://github.com/tueda/donuts'
        inceptionYear = '2019'
        licenses {
          license {
            name = 'MIT License'
            url = 'http://www.opensource.org/licenses/mit-license.php'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            id = 'tueda'
            name = 'Takahiro Ueda'
            email = 'tueda@st.seikei.ac.jp'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/tueda/donuts.git'
          developerConnection = 'scm:git:https://github.com/tueda/donuts.git'
          url = 'https://github.com/tueda/donuts'
        }
      }
    }
  }
  repositories {
    maven {
      url = "${buildDir}/repo"
    }
  }
}

// See researchgate/gradle-release#107
project.tasks.release.dependsOn check

release {
  tagTemplate = 'v${version}'
}
