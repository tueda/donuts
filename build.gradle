plugins {
  id 'jacoco'
  id 'java-library'
  id 'com.diffplug.gradle.spotless' version '3.24.2'
  id 'com.github.ben-manes.versions' version '0.25.0'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
  id 'ru.vyarus.quality' version '3.4.0'
}

group = 'com.github.tueda'
archivesBaseName = 'donuts'
version = '0.0.1'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencies {
  api 'cc.redberry:rings:2.5.3'
  compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.0-beta4'
  compileOnly 'org.projectlombok:lombok:1.18.10'
  annotationProcessor 'org.projectlombok:lombok:1.18.10'
  testImplementation 'com.google.truth:truth:1.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

javadoc {
  configure(options) {
    links += [
      'https://docs.oracle.com/javase/8/docs/api/',
      'https://static.javadoc.io/cc.redberry/rings/2.5.3/',
    ]
    source = '8'
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat('1.7')
  }
}

configurations {
  checkstyleConfig
}

dependencies {
  checkstyleConfig('com.puppycrawl.tools:checkstyle:8.24') {
    transitive = false
  }
}

quality {
  checkstyleVersion = '8.24'
  pmdVersion = '6.17.0'
  spotbugsVersion = '4.0.0-beta3'

  spotbugsEffort = 'max'
  spotbugsLevel = 'low'
}
afterEvaluate {
  checkstyle {
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  }
  pmd {
    incrementalAnalysis = true
    ruleSetFiles = files('config/pmd.xml')
  }
}
