plugins {
  id 'java-library'
  id 'com.diffplug.gradle.spotless' version '3.23.0'
  id 'ru.vyarus.quality' version '3.4.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

dependencies {
  api 'cc.redberry:rings:2.5.2'
  compileOnly 'com.github.spotbugs:spotbugs-annotations:4.0.0-beta1'
  testImplementation 'com.google.truth:truth:0.44'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

javadoc {
  configure(options) {
    links += [
      'https://docs.oracle.com/javase/8/docs/api/',
      'https://static.javadoc.io/cc.redberry/rings/2.5.2/',
    ]
  }
}

test {
  useJUnitPlatform()
}

spotless {
  java {
    googleJavaFormat('1.7')
  }
}

configurations {
  checkstyleConfig
}

dependencies {
  checkstyleConfig('com.puppycrawl.tools:checkstyle:8.20') {
    transitive = false
  }
}

quality {
  checkstyleVersion = '8.20'
  pmdVersion = '6.14.0'
  spotbugsVersion = '4.0.0-beta1'

  spotbugsEffort = 'max'
  spotbugsLevel = 'low'
}
afterEvaluate {
  checkstyle {
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  }
  pmd {
    ruleSets = []
    ruleSetFiles = files()
    ruleSetConfig = resources.text.fromString('''<?xml version="1.0"?>
      <ruleset
        name="Custom Rules"
        xmlns="http://pmd.sourceforge.net/ruleset/2.0.0"
        xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
        xsi:schemaLocation="http://pmd.sourceforge.net/ruleset/2.0.0 https://pmd.sourceforge.io/ruleset_2_0_0.xsd">
        <description>Our custom rules</description>
        <rule ref="category/java/bestpractices.xml" ></rule>
        <rule ref="category/java/design.xml" >
          <exclude name="LoosePackageCoupling" />
        </rule>
        <rule ref="category/java/documentation.xml" ></rule>
        <rule ref="category/java/errorprone.xml" >
          <exclude name="BeanMembersShouldSerialize" />
        </rule>
        <rule ref="category/java/multithreading.xml" ></rule>
        <rule ref="category/java/performance.xml" ></rule>
        <rule ref="category/java/security.xml" ></rule>
      </ruleset>
    ''')
  }
}
